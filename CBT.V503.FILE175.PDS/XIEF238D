*          DATA SET XIEF238DCM AT LEVEL 003 AS OF 03/29/90
XIEF238D TITLE 'LIGHTING MESSAGE HANDLING EXIT            '             00001**2
*/* LIB:  N/A                                                           00002
*/* GDE:  GC28-1147 MVS/XA SPL USER EXITS                               00003
*/* DOC:  THIS EXIT IS TAKEN FROM THE CANCWAIT SAMPLE ON PAGE 51        00004
*/*       ON THE JOB LOG WITH STEP TERMINATION STATUS.                  00005
*********************************************************************** 00006
*                                                                     * 00007
*             MODULE NAME = XIEF238D                                  * 00008**2
*                                                                     * 00009
*             DESCRIPTIVE NAME =  COMMUNICATIONS TASK USER EXIT FOR   * 00010
*                MESSAGE IEF238D                                      * 00011**2
*                                                                     * 00012
*             PHILIPS LIGHTING 1990, THIS IS IN THE PUBLIC DOMAIN AND * 00013
*             IS IN USE AT PHILIPS LIGHTING.  PLEASE FEEL FREE TO USE * 00014
*             AT YOUR OWN RISK.  NO WARRANTY EXPRESS OR IMPLIED IS    * 00015**3
*             GIVEN                                                   * 00016**3
*                                                                     * 00017**3
*             FUNCTION =                                              * 00018
*                REPLY WAIT IF THE MESSAGE ALLOWS WAIT AS A REPLY     * 00019**2
*                                                                     * 00020
*                                                                     * 00021
*              NOTES =                                                * 00022
*                                                                     * 00023
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE * 00024
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          * 00025
*                                                                     * 00026
*                 RESTRICTIONS = NONE                                 * 00027
*                                                                     * 00028
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        * 00029
*                    REGISTERS 0 TO 1  = WORK REGISTERS               * 00030
*                    REGISTERS 2 TO 11 = SEE REGISTER EQUATES BELOW   * 00031**3
*                    REGISTER  12      = ADDRESSABILITY TO XIEF238D   * 00032**2
*                                        CSECT                        * 00033
*                    REGISTER  13      = ADDRESSIBILITY TO DATA DSECT * 00034
*                    REGISTERS 14,15   = WORK REGISTERS               * 00035
*                                                                     * 00036
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        * 00037
*                   BINARY ZEROES)                                    * 00038
*                                                                     * 00039
*             MODULE TYPE = PROCEDURE                                 * 00040
*                                                                     * 00041
*                PROCESSOR = ASM                                      * 00042
*                                                                     * 00043
*                ATTRIBUTES = KEY 0, REENTRANT,                       * 00044
*                   SUPERVISOR STATE, ENABLED, NO LOCKS HELD          * 00045
*                                                                     * 00046
*             ENTRY POINTS = XIEF238D (ONLY ENTRY POINT)              * 00047**2
*                                                                     * 00048
*                LINKAGE =                                            * 00049
*                                                                     * 00050
*                                                                     * 00051
*             INPUT = REG1 POINTS TO THE ADDRESS OF THE               * 00052
*                     COMMUNICATIONS TASK EXIT PARAMETER LIST (CTXT)  * 00053
*                     REGS 0 AND 2-12 IRRELEVANT                        00054
*                     REG 13, STANDARD SAVE AREA                        00055
*                     REG 14, RETURN ADDRESS                            00056
*                     REG 15, ENTRY POINT ADDRESS                       00057
*                                                                     * 00058
*             OUTPUT = NONE                                           * 00059
*                                                                     * 00060
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   * 00061
*                                                                     * 00062
*                OUTPUT = NONE                                        * 00063
*                                                                     * 00064
*                RETURN CODE = ZERO                                   * 00065
*                                                                     * 00066
*             EXIT - ERROR = NONE                                     * 00067
*                                                                     * 00068
*                OUTPUT = NONE                                        * 00069
*                                                                     * 00070
*                RETURN CODE = ZERO                                   * 00071
*                                                                     * 00072
*             EXTERNAL REFERENCES =                                   * 00073
*                                                                     * 00074
*                ROUTINES = NONE                                      * 00075
*                                                                     * 00076
*                DATA AREAS = NONE                                    * 00077
*                                                                     * 00078
*                CONTROL BLOCKS = NONE                                * 00079
*                                                                     * 00080
*             TABLES = NONE                                           * 00081
*                                                                     * 00082
*             MACROS = SYS1.MACLIB FREEMAIN, GETMAIN, IEZBITS,        * 00083**3
*                          IEZMGCR, PANAEXIT, RETURN, SAVE            * 00084**3
*                      SYS1.AMODGEN IEZVX100, MGCR                    * 00085**3
*                                                                     * 00086**3
*             CHANGE ACTIVITY = NONE                                  * 00087
*                                                                     * 00088
*                                                                     * 00089
*             ABEND CODES = NONE                                      * 00090
*                                                                     * 00091
*********************************************************************** 00092
         EJECT                                                          00093
XIEF238D CSECT ,                                                        00094**2
XIEF238D AMODE 31                                                       00095**2
XIEF238D RMODE ANY                                                      00096**2
*                                                                       00097
* CONSTANT ADDRESS EQAUTES                                              00098
*                                                                       00099
PSAAOLD  EQU    X'224',4    ADDRESS IN PSA OF CURRENT ASCB              00100
ASCBJBNI EQU    X'AC',4     ADDRESS IN JOBNAME IN ASCB                  00101
CHSTEP   EQU    X'38',8     ADDRESS IN STEP NAME IN CSCB                00102
CHPROCSN EQU    X'18',8     ADDRESS IN PROC STEP NAME IN CSCB           00103
*                                                                       00104
* CONSTANT EQUATES                                                      00105
*                                                                       00106
K0       EQU   00                  NON-RELOCATABLE CONSTANT 00          00107
K1       EQU   01                  NON-RELOCATABLE CONSTANT 01          00108
K2       EQU   02                  NON-RELOCATABLE CONSTANT 02          00109
K3       EQU   03                  NON-RELOCATABLE CONSTANT 03          00110
K4       EQU   04                  NON-RELOCATABLE CONSTANT 04          00111
K5       EQU   05                  NON-RELOCATABLE CONSTANT 05          00112
K6       EQU   06                  NON-RELOCATABLE CONSTANT 06          00113
K7       EQU   07                  NON-RELOCATABLE CONSTANT 07          00114
K8       EQU   08                  NON-RELOCATABLE CONSTANT 08          00115
K9       EQU   09                  NON-RELOCATABLE CONSTANT 09          00116
KA       EQU   10                  NON-RELOCATABLE CONSTANT 10          00117
KB       EQU   11                  NON-RELOCATABLE CONSTANT 11          00118
KC       EQU   12                  NON-RELOCATABLE CONSTANT 12          00119
KD       EQU   13                  NON-RELOCATABLE CONSTANT 13          00120
KE       EQU   14                  NON-RELOCATABLE CONSTANT 14          00121
KF       EQU   15                  NON-RELOCATABLE CONSTANT 15          00122
*                                                                       00123
* CONSTANT EQUATES                                                      00124
*                                                                       00125
SPINPRVT EQU   230                 USE SUBPOOL 230 FOR GETMAIN          00126**5
SUBPOOL  EQU   252                 USE SUBPOOL 252 FOR GETMAIN          00127**5
CK       EQU   C'K'                CONSTANT CHAR 'K'                    00128
CASTR    EQU   C'*'                CONSTANT CHAR '*'                    00129
C0       EQU   C'0'                CONSTANT CHAR '0'                    00130
         IEZBITS                                                        00131
*                                                                       00132
* REGISTER EQUATES                                                      00133
*                                                                       00134
R0       EQU   00                  REG  0 - WORK REG                    00135**3
R1       EQU   01                  REG  1 - WORK/PARMETER REG           00136**3
R2       EQU   02                  REG  2 - WORK REG                    00137**3
R3       EQU   03                  REG  3 - POINTER TO THE CTXT         00138**3
R4       EQU   04                  REG  4 - PTR TO THE MSSG ATTRIBUTES  00139**3
R5       EQU   05                  REG  5 - PTR TO THE MESSAGE AREA     00140**3
R6       EQU   06                  REG  6 - POINTER TO THE REPLY        00141**3
R7       EQU   07                  REG  7 - PTR TO MSSG ROUTING AREA    00142**3
R8       EQU   08                  REG  8 - USED FOR EXECUTE TO MV REPL 00143**3
R9       EQU   09                  REG  9 - UNUSED                      00144**3
R10      EQU   10                  REG 10 - UNUSED                      00145
R11      EQU   11                  REG 11 - UNUSED                      00146
R12      EQU   12                  REG 12 - CSECT BASE REG              00147
R13      EQU   13                  REG 13 - DATA DSECT BASE REG         00148
R14      EQU   14                  REG 14 - LINKAGE REGISTER            00149
R15      EQU   15                  REG 15 - WORK REG                    00150
         EJECT                                                          00151
         SAVE  (14,12)                                                  00152**5
         USING XIEF238D,R12        SET UP CSECT ADDRESSABILITY          00153**2
         LR    R12,R15             LOAD BASE REGISTER                   00154
         L     R3,0(,R1)           LOAD ADDRESS OF CTXT                 00155**1
         USING CTXT,R3                                                  00156**1
         L     R4,CTXTTXPJ         POINT TO THE MESSAGE ATTRIBUTES      00157**5
         USING CTXTATTR,R4                                              00158**1
         LA    R5,CTXTTMSG         POINT TO THE MESSAGE AREA            00159**7
         USING MSGTEXT,R5                                               00160**4
         L     R7,CTXTRCP          POINT TO MESSAGE ROUTING AREA        00161**5
         USING CTXTROUT,R7                                              00162**5
         CLC   WAITVAL,WAITCON      IS THERE A WAIT OPTION?             00163*17
         BNE   EXIT                 NO, GO TO EXIT                      00164*16
         LA    R6,WAITMSG           YES, SET UP TO REPLY WAIT           00165*10
*********************************************************************** 00166**1
*                                                                     * 00167**1
*        DYNAMIC STORAGE FOR THIS MODULE IS BEING OBTAINED BELOW THE  * 00168**1
*        16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR PARAMETER LIST  * 00169**1
*        TO BE IN 24-BIT ADDRESSABLE STORAGE                          * 00170**1
*                                                                     * 00171**1
*********************************************************************** 00172**1
         GETMAIN RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW OBTAIN DYNAMIC     00173**1
*                                                    STORAGE            00174**1
         USING DATAAREA,R1                                              00175**1
         ST    R13,SAVEPR13     SAVE THE SAVE AREA POINTER IN THE NEW   00176**4
         ST    R1,8(,R13)           AREA AND R1 IN THE PRIOR ONE        00177**4
         LR    R13,R1 POINT R13 TO THE DATA AREA                        00178**4
         DROP  R1                                                       00179**1
         USING DATAAREA,R13                                             00180**1
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR OUT THE REPLY AREA         00181**4
         XC    CTXTROUT(2),CTXTROUT    CLEAR OUT THE ROUT CODE          00182**4
         SLR   R8,R8                   SET UP TO GET THE LENGTH OF      00183*10
         IC    R8,0(,R6)                   THE REPLY TEXT               00184*10
         EX    R8,MVREPLY              MOVE THE REPLY TEXT              00185*10
         MVC   REPLY,CTXTRPID          MOVE IN THE REPLY ID             00186*10
         LA    R8,MGCRTEXT-MGCRPL+1(,R8)   SET UP THE LENGTH OF THE     00187*10
         STC   R8,MGCRLGTH                     REPLY                    00188*10
         SLR   R0,R0                                                    00189**2
         MGCR  MGCRPL                  ISSUE THE MESSAGE                00190**4
         LR    R8,R13                  RESTORE THE SAVE AREA POINTER    00191*15
         L     R13,SAVEPR13                                             00192**4
         FREEMAIN RU,LV=DATAEND,A=(R8),SP=SPINPRVT  FREE THE DATA AREA  00193*15
         B     CNGROUT                                                  00194**6
MVREPLY  MVC   MGCRTEXT(0),1(R6)       EXECUTED MOVE OF REPLY TEXT      00195**6
         DROP  R13                                                      00196**6
CNGROUT  DS    0H                                                       00197**2
         OI    CTXTRFB1,CTXTRCRC       REQUEST TO CHANGE ROUT           00198*11
EXIT     DS    0H                                                       00199**2
         RETURN (14,12)                EXIT, NOTE RETURN CODE NOT GIVEN 00200**4
WAITMSG  DS    0CL(L'WAITLGTH+L'WAITTXT)                                00201**2
WAITLGTH DC    AL1(L'WAITTXT-1)                                         00202*10
WAITTXT  DC    C'REPLY XX,WAIT'                                         00203**8
WAITCON  DC    C'WAIT'                                                  00204*14
DATAAREA DSECT                                                          00205**2
         DS    0D                                                       00206**2
SAVEAREA DS    0CL72                                                    00207**2
SAVEPL1  DS    F                                                        00208**2
SAVEPR13 DS    F                                                        00209**2
SAVENX13 DS    F                                                        00210**2
SAVE1412 DS    15F                                                      00211**2
         DS    0F                                                       00212**2
MGCR     IEZMGCR DSECT=NO                                               00213**2
         ORG   MGCRTEXT                                                 00214**2
COMMAND  DS    CL6                                                      00215**2
REPLY    DS    CL2                                                      00216**2
REPLYMSG DS    CL50                                                     00217**2
         ORG                                                            00218**2
DATAEND  EQU   *-DATAAREA                                               00219**2
MSGTEXT  DSECT                                                          00220**2
MSGID    DS    CL8                                                      00221**2
JOBDATA  DS    CL8                                                      00222**2
         ORG   MSGTEXT                                                  00223**2
         DS    CL4                                                      00224**2
WTORID   DS    CL8                                                      00225**2
         DS    CL25                                                     00226*14
WAITVAL  DS    CL4                                                      00227*17
         IEZVX100                                                       00228**2
         END                                                            00229**2
