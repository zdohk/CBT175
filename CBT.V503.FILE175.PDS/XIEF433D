*          DATA SET XIEF433DCM AT LEVEL 003 AS OF 03/29/90
XIEF433D TITLE 'LIGHTING MESSAGE HANDLING EXIT            '             00001**2
*/* LIB:  N/A                                                           00002
*/* GDE:  GC28-1147 MVS/XA SPL USER EXITS                               00003
*/* DOC:  THIS EXIT IS TAKEN FROM THE CANCWAIT SAMPLE ON PAGE 51        00004
*/*       ON THE JOB LOG WITH STEP TERMINATION STATUS.                  00005
*********************************************************************** 00006
*                                                                     * 00007
*             MODULE NAME = XIEF433D                                  * 00008**2
*                                                                     * 00009
*             DESCRIPTIVE NAME =  COMMUNICATIONS TASK USER EXIT FOR   * 00010
*                MESSAGE IEF433D                                      * 00011**2
*                                                                     * 00012
*             PHILIPS LIGHTING 1990, THIS IS IN THE PUBLIC DOMAIN AND * 00013
*             IS IN USE AT PHILIPS LIGHTING.  PLEASE FEEL FREE TO USE * 00014
*             AT YOUR OWN RISK.  NO WARRANTY EXPRESS OR IMPLIED IS    * 00015**3
*             GIVEN                                                   * 00016**3
*                                                                     * 00017**3
*             FUNCTION =                                              * 00018
*                AUTOMATICALLY ISSUE NOHOLD REPLY TO IEF433D          * 00019**2
*                                                                     * 00020
*              NOTES =                                                * 00021
*                                                                     * 00022
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE * 00023
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          * 00024
*                                                                     * 00025
*                 RESTRICTIONS = NONE                                 * 00026
*                                                                     * 00027
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        * 00028
*                    REGISTERS 0 TO 1  = WORK REGISTERS               * 00029
*                    REGISTERS 2 TO 11 = SEE REGISTER EQUATES BELOW   * 00030**3
*                    REGISTER  12      = ADDRESSABILITY TO XIEF433D   * 00031**2
*                                        CSECT                        * 00032
*                    REGISTER  13      = ADDRESSIBILITY TO DATA DSECT * 00033
*                    REGISTERS 14,15   = WORK REGISTERS               * 00034
*                                                                     * 00035
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        * 00036
*                   BINARY ZEROES)                                    * 00037
*                                                                     * 00038
*             MODULE TYPE = PROCEDURE                                 * 00039
*                                                                     * 00040
*                PROCESSOR = ASM                                      * 00041
*                                                                     * 00042
*                ATTRIBUTES = KEY 0, REENTRANT,                       * 00043
*                   SUPERVISOR STATE, ENABLED, NO LOCKS HELD          * 00044
*                                                                     * 00045
*             ENTRY POINTS = XIEF433D (ONLY ENTRY POINT)              * 00046**2
*                                                                     * 00047
*                LINKAGE =                                            * 00048
*                                                                     * 00049
*                                                                     * 00050
*             INPUT = REG1 POINTS TO THE ADDRESS OF THE               * 00051
*                     COMMUNICATIONS TASK EXIT PARAMETER LIST (CTXT)  * 00052
*                     REGS 0 AND 2-12 IRRELEVANT                        00053
*                     REG 13, STANDARD SAVE AREA                        00054
*                     REG 14, RETURN ADDRESS                            00055
*                     REG 15, ENTRY POINT ADDRESS                       00056
*                                                                     * 00057
*             OUTPUT = NONE                                           * 00058
*                                                                     * 00059
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   * 00060
*                                                                     * 00061
*                OUTPUT = NONE                                        * 00062
*                                                                     * 00063
*                RETURN CODE = ZERO                                   * 00064
*                                                                     * 00065
*             EXIT - ERROR = NONE                                     * 00066
*                                                                     * 00067
*                OUTPUT = NONE                                        * 00068
*                                                                     * 00069
*                RETURN CODE = ZERO                                   * 00070
*                                                                     * 00071
*             EXTERNAL REFERENCES =                                   * 00072
*                                                                     * 00073
*                ROUTINES = NONE                                      * 00074
*                                                                     * 00075
*                DATA AREAS = NONE                                    * 00076
*                                                                     * 00077
*                CONTROL BLOCKS = NONE                                * 00078
*                                                                     * 00079
*             TABLES = NONE                                           * 00080
*                                                                     * 00081
*             MACROS = SYS1.MACLIB FREEMAIN, GETMAIN, IEZBITS,        * 00082**3
*                          IEZMGCR, PANAEXIT, RETURN, SAVE            * 00083**3
*                      SYS1.AMODGEN IEZVX100, MGCR                    * 00084**3
*                                                                     * 00085**3
*             CHANGE ACTIVITY = NONE                                  * 00086
*                                                                     * 00087
*                                                                     * 00088
*             ABEND CODES = NONE                                      * 00089
*                                                                     * 00090
*********************************************************************** 00091
         EJECT                                                          00092
XIEF433D CSECT ,                                                        00093**2
XIEF433D AMODE 31                                                       00094**2
XIEF433D RMODE ANY                                                      00095**2
*                                                                       00096
* CONSTANT ADDRESS EQAUTES                                              00097
*                                                                       00098
PSAAOLD  EQU    X'224',4    ADDRESS IN PSA OF CURRENT ASCB              00099
ASCBJBNI EQU    X'AC',4     ADDRESS IN JOBNAME IN ASCB                  00100
CHSTEP   EQU    X'38',8     ADDRESS IN STEP NAME IN CSCB                00101
CHPROCSN EQU    X'18',8     ADDRESS IN PROC STEP NAME IN CSCB           00102
*                                                                       00103
* CONSTANT EQUATES                                                      00104
*                                                                       00105
K0       EQU   00                  NON-RELOCATABLE CONSTANT 00          00106
K1       EQU   01                  NON-RELOCATABLE CONSTANT 01          00107
K2       EQU   02                  NON-RELOCATABLE CONSTANT 02          00108
K3       EQU   03                  NON-RELOCATABLE CONSTANT 03          00109
K4       EQU   04                  NON-RELOCATABLE CONSTANT 04          00110
K5       EQU   05                  NON-RELOCATABLE CONSTANT 05          00111
K6       EQU   06                  NON-RELOCATABLE CONSTANT 06          00112
K7       EQU   07                  NON-RELOCATABLE CONSTANT 07          00113
K8       EQU   08                  NON-RELOCATABLE CONSTANT 08          00114
K9       EQU   09                  NON-RELOCATABLE CONSTANT 09          00115
KA       EQU   10                  NON-RELOCATABLE CONSTANT 10          00116
KB       EQU   11                  NON-RELOCATABLE CONSTANT 11          00117
KC       EQU   12                  NON-RELOCATABLE CONSTANT 12          00118
KD       EQU   13                  NON-RELOCATABLE CONSTANT 13          00119
KE       EQU   14                  NON-RELOCATABLE CONSTANT 14          00120
KF       EQU   15                  NON-RELOCATABLE CONSTANT 15          00121
*                                                                       00122
* CONSTANT EQUATES                                                      00123
*                                                                       00124
SPINPRVT EQU   230                 USE SUBPOOL 230 FOR GETMAIN          00125**5
SUBPOOL  EQU   252                 USE SUBPOOL 252 FOR GETMAIN          00126**5
CK       EQU   C'K'                CONSTANT CHAR 'K'                    00127
CASTR    EQU   C'*'                CONSTANT CHAR '*'                    00128
C0       EQU   C'0'                CONSTANT CHAR '0'                    00129
         IEZBITS                                                        00130
*                                                                       00131
* REGISTER EQUATES                                                      00132
*                                                                       00133
R0       EQU   00                  REG  0 - WORK REG                    00134**3
R1       EQU   01                  REG  1 - WORK/PARMETER REG           00135**3
R2       EQU   02                  REG  2 - WORK REG                    00136**3
R3       EQU   03                  REG  3 - POINTER TO THE CTXT         00137**3
R4       EQU   04                  REG  4 - PTR TO THE MSSG ATTRIBUTES  00138**3
R5       EQU   05                  REG  5 - PTR TO THE MESSAGE AREA     00139**3
R6       EQU   06                  REG  6 - POINTER TO THE REPLY        00140**3
R7       EQU   07                  REG  7 - PTR TO MSSG ROUTING AREA    00141**3
R8       EQU   08                  REG  8 - USED BY EXECUTED MOVE       00142**3
R9       EQU   09                  REG  9 - UNUSED                      00143**3
R10      EQU   10                  REG 10 - UNUSED                      00144
R11      EQU   11                  REG 11 - UNUSED                      00145
R12      EQU   12                  REG 12 - CSECT BASE REG              00146
R13      EQU   13                  REG 13 - DATA DSECT BASE REG         00147
R14      EQU   14                  REG 14 - LINKAGE REGISTER            00148
R15      EQU   15                  REG 15 - WORK REG                    00149
         EJECT                                                          00150
         SAVE  (14,12)                                                  00151**5
         USING XIEF433D,R12        SET UP CSECT ADDRESSABILITY          00152**2
         LR    R12,R15             LOAD BASE REGISTER                   00153
         L     R3,0(,R1)           LOAD ADDRESS OF CTXT                 00154**1
         USING CTXT,R3                                                  00155**1
         L     R4,CTXTTXPJ         POINT TO THE MESSAGE ATTRIBUTES      00156**5
         USING CTXTATTR,R4                                              00157**1
         LA    R5,CTXTTMSG         POINT TO THE MESSAGE AREA            00158**7
         USING MSGTEXT,R5                                               00159**4
         L     R7,CTXTRCP          POINT TO MESSAGE ROUTING AREA        00160**5
         USING CTXTROUT,R7                                              00161**5
         LA    R6,NOHLDMSG          YES, SET UP TO REPLY NOHOLD         00162*10
*********************************************************************** 00163**1
*                                                                     * 00164**1
*        DYNAMIC STORAGE FOR THIS MODULE IS BEING OBTAINED BELOW THE  * 00165**1
*        16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR PARAMETER LIST  * 00166**1
*        TO BE IN 24-BIT ADDRESSABLE STORAGE                          * 00167**1
*                                                                     * 00168**1
*********************************************************************** 00169**1
         GETMAIN RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW OBTAIN DYNAMIC     00170**1
*                                                    STORAGE            00171**1
         USING DATAAREA,R1                                              00172**1
         ST    R13,SAVEPR13     SAVE THE SAVE AREA POINTER IN THE NEW   00173**4
         ST    R1,8(,R13)           AREA AND R1 IN THE PRIOR ONE        00174**4
         LR    R13,R1 POINT R13 TO THE DATA AREA                        00175**4
         DROP  R1                                                       00176**1
         USING DATAAREA,R13                                             00177**1
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR OUT THE REPLY AREA         00178**4
         XC    CTXTROUT(2),CTXTROUT    CLEAR OUT THE ROUT CODE          00179**4
         SLR   R8,R8                   SET UP TO GET THE LENGTH OF      00180*10
         IC    R8,0(,R6)                   THE REPLY TEXT               00181*10
         EX    R8,MVREPLY              MOVE THE REPLY TEXT              00182*10
         MVC   REPLY,CTXTRPID          MOVE IN THE REPLY ID             00183*10
         LA    R8,MGCRTEXT-MGCRPL+1(,R8)   SET UP THE LENGTH OF THE     00184*10
         STC   R8,MGCRLGTH                     REPLY                    00185*10
         SLR   R0,R0                                                    00186**2
         MGCR  MGCRPL                  ISSUE THE MESSAGE                00187**4
         LR    R8,R13                  RESTORE THE SAVE AREA POINTER    00188*15
         L     R13,SAVEPR13                                             00189**4
         FREEMAIN RU,LV=DATAEND,A=(R8),SP=SPINPRVT  FREE THE DATA AREA  00190*15
         B     CNGROUT                                                  00191**6
MVREPLY  MVC   MGCRTEXT(0),1(R6)       EXECUTED MOVE OF REPLY TEXT      00192**6
         DROP  R13                                                      00193**6
CNGROUT  DS    0H                                                       00194**2
         OI    CTXTRFB1,CTXTRCRC       REQUEST TO CHANGE ROUT           00195*11
EXIT     DS    0H                                                       00196**2
         RETURN (14,12)                EXIT, NOTE RETURN CODE NOT GIVEN 00197**4
IEF433D  DC    CL7'IEF433D'                                             00198**2
NOHLDMSG DS    0CL(L'NOHLLGTH+L'NOHLTXT)                                00199**2
NOHLLGTH DC    AL1(L'NOHLTXT-1)                                         00200*10
NOHLTXT  DC    C'REPLY XX,NOHOLD'                                       00201**8
DATAAREA DSECT                                                          00202**2
         DS    0D                                                       00203**2
SAVEAREA DS    0CL72                                                    00204**2
SAVEPL1  DS    F                                                        00205**2
SAVEPR13 DS    F                                                        00206**2
SAVENX13 DS    F                                                        00207**2
SAVE1412 DS    15F                                                      00208**2
         DS    0F                                                       00209**2
MGCR     IEZMGCR DSECT=NO                                               00210**2
         ORG   MGCRTEXT                                                 00211**2
COMMAND  DS    CL6                                                      00212**2
REPLY    DS    CL2                                                      00213**2
REPLYMSG DS    CL50                                                     00214**2
         ORG                                                            00215**2
DATAEND  EQU   *-DATAAREA                                               00216**2
MSGTEXT  DSECT                                                          00217**2
MSGID    DS    CL8                                                      00218**2
JOBDATA  DS    CL8                                                      00219**2
         ORG   MSGTEXT                                                  00220**2
         DS    CL4                                                      00221**2
WTORID   DS    CL8                                                      00222**2
         DS    CL25                                                     00223*14
WAITVAL  DS    CL4                                                      00224*17
         IEZVX100                                                       00225**2
         END                                                            00226**2
