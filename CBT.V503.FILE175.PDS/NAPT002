++ USERMOD (NAPT002) .
++ VER (Z038) FMID(HTE21C2).
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(IKJEFF10),DISP=SHR
//SYSIN  DD  SYS1.USERSRC(IKJEFF10),DISP=SHR
++SRC (IKJEFF10) DISTLIB(USERSRC).
*          DATA SET IKJEFF10   AT LEVEL 003 AS OF 12/09/85
IKJEFF10 CSECT ,                                                NAPT002 00001
*** START --- NORTH AMERICAN PHILIPS LIGHTING MOD PROLOG ------ NAPT002 00002
***                                                             NAPT002 00003
*** FUNCTION.                                                   NAPT002 00004
***       1. ALLOW UTILITY JOBS BY RECOGNIZING A SPECIAL USER   NAPT002 00005
***          ID OF $$$$$$ WHICH WOULD BE CHANGED TO THE         NAPT002 00006
***          SUBMITTERS ID WHEN IT IS FOUND IN THE JOB NAME     NAPT002 00007
***          AND WHICH WILL BE SEARCHED FOR AND REPLACED        NAPT002 00008
***          ANYWHERE ELSE IT OCCURS IN THE JCL (BUT NOT THE    NAPT002 00009
***          DATA.                                              NAPT002 00010
***                                                             NAPT002 00011
***       2. DISALLOW SUBMISSION OF JOBS WITH A MESSAGE CLASS   NAPT002 00012
***          OR SYOUT OF X OR Y  WHICH CANNOT BE LOOKED AT BY   NAPT002 00013
***          THE SUBMITTER.  (FCH, FCI, CURRENT FIRST SHIFT     NAPT002 00014
***          OPERATOR AND GENERIC OPERATOR SIGNONS CAN SUBMIT   NAPT002 00015
***          JOBS WITH ANY JOB NAME, TEST JOBS WILL BE FORCED   NAPT002 00016
***          TO 7 CHRS IF THEY ARE 6 CHRS AND NOT THE SUBMITTER NAPT002 00017
***          LEGAL JOB NAMES ARE THE SUBMITTERS-ID OR THE       NAPT002 00018
***          SUBMITTER'S ID PLUS 1 CHARACTER.                   NAPT002 00019
***                                                             NAPT002 00020
*** DEPENDENCIES.                                               NAPT002 00021
***                                                             NAPT002 00022
***         1. NORMAL LINKAGE CONVENTIONS ARE USED.             NAPT002 00023
***         2. THE MODULE IS RE-ENTRANT AND DOES NOT CALL ANY   NAPT002 00024
***            SUBROUTINES OR USE SYSTEM SERVICES OTHER THAN    NAPT002 00025
***            GETMAIN/FREEMAIN                                 NAPT002 00026
***                                                             NAPT002 00027
*** REGISTER CONVENTIONS.                                       NAPT002 00028
***                                                             NAPT002 00029
***         R1  - AFFECTED BY TRT INSTRUCTION                   NAPT002 00030
***         R2  - AFFECTED BY TRT INSTRUCTION                   NAPT002 00031
***         R3  - POINTER TO SUBMITTER'S USER-ID                NAPT002 00032
***         R4  - NOT USED                                      NAPT002 00033
***         R5  - WORK REGISTER FOR SIZE OF FIELDS              NAPT002 00034
***         R6  - POINTER TO THE CARD IMAGE                     NAPT002 00035
***         R7  - BASE REGISTER FOR SUBMIT SECONDARY DSECT      NAPT002 00036
***         R8  - POINTER TO THE MESSAGE AREA (TO/FROM USER)    NAPT002 00037
***         R9  - BASE REGISTER FOR SUBMIT DSECT                NAPT002 00038
***         R10 - BASE REGISTER FOR SUBROUTINE                  NAPT002 00039
***         R11 - SIZE REGISTER FOR FIELD SCANS                 NAPT002 00040
***         R12 - POINTER TO FIELD TO BE SCANNED                NAPT002 00041
***                                                             NAPT002 00042
*** CONTROL BLOCKS. (MAPPING MACROS FOLLOWED BY ATTRIBUTE)      NAPT002 00043
***                                                             NAPT002 00044
***         IKJEFFIE - TYPE=SUBMIT GENERATES                    NAPT002 00045
***             IEDSECTD - GENERAL POINTERS AND FLAGS           NAPT002 00046
***             IESUBCTD - SUBMIT POINTERS AND FLAGS            NAPT002 00047
***                                                             NAPT002 00048
***                                                             NAPT002 00049
*** CHANGE ACTIVITY.                                            NAPT002 00050
***                                                             NAPT002 00051
***      CHANGES BY C F MORRIS, WESTINGHOUSE LAMP               NAPT002 00052
***    1. REMOVE THE GETMAIN AND FREEMAIN FOR THE SAVE AREA     NAPT002 00053
***       SINCE THEY ARE NOT NEEDED BECAUSE NOTHING IS          NAPT002 00054
***       CALLED.                                               NAPT002 00055
***    2. CHECK FOR 6 CHR USER-ID PLUS 1 CHR OR 6 CHR USER-ID   NAPT002 00056
***       EQUAL TO THAT OF THE SUBMITTER OR 6 QUESTION MARKS    NAPT002 00057
***       (??????) PLUS 1 CHR AS VALID JOB NAME.  IF 6          NAPT002 00058
***       QUESTION MARKS REPLACE WITH USER-ID OF SUBMITTER AND  NAPT002 00059
***       AND TURN ON SWITCHES TO SCAN FOR ?????? IN ALL JCL    NAPT002 00060
***       REMOVE GENERAL DYNAMICS CODE.                         NAPT002 00061
***    CHANGES 1-23 TO GET THE EXIT CODED RIGHT                 NAPT002 00062
***    CHANGES 24-28 TO JUST REJECT JOBS WHICH GO TO MSGCLASS   NAPT002 00063
***        X AND LACK THE SUBMITTERS ID IN THE JOB NAME AND     NAPT002 00064
***        TO TREAT $$$$$$ THE SAME AS 6 QUESTION MARKS.        NAPT002 00065
***    CHANGES 29-34 TO REJECT JOBS WHICH CONTAIN MSGCLASS=X    NAPT002 00066
***        OR SYSOUT=X AND WHICH ARE NOT SUBMITTED BY           NAPT002 00067
***        AUTHORIZED SUBMITTERS, ALLOW AUTHORIZED SUBMITTERS   NAPT002 00068
***        TO SUBMIT IF TSO USER-ID FORMATTED JOB NAME, AND     NAPT002 00069
***        VERIFY SUBMISSION DESIRED OF NON-TSO JOB BY          NAPT002 00070
***        AUTHORIZED SUBMITTERS.                               NAPT002 00071
***                                                             NAPT002 00072
***    02/15/85-02/19/85 - FORCE A JCL ERROR ON THE CARD WITH   NAPT002 00073
***        THE OFFENDING MESSAGE (SYSOUT) CLASS, IF CARD NOT    NAPT002 00074
***        FLAGGED AS A JOB CARD, ALSO REPEAT THE SUBMIT        NAPT002 00075
***        FAILURE MESSAGE.  I WOULD HAVE PURGED THE JOB BUT    NAPT002 00076
***        THE DEVELOPERS OF JES3 IN THEIR INFINITE LACK OF     NAPT002 00077
***        WISDOM AND REFUSAL OF JES2 COMPATIBILITY DO NOT      NAPT002 00078
***        RECOGNIZE THE /*PURGE                                NAPT002 00079
***                                                             NAPT002 00080
***   03/07/85                                                  NAPT002 00081
***        JES3 MODIFIED TO USE /*PURGE BY RECOGNITION IN       NAPT002 00082
***        IATUX33 AND SETTING OPERATOR CANCEL NO PRINT BIT     NAPT002 00083
***                                                             NAPT002 00084
***   06/21/85                                                  NAPT002 00085
***        REMOVE JCLERROR CONSTANT AND CHANGE SMP MOD ID TO    NAPT002 00086
***        NAPT002                                              NAPT002 00087
***                                                             NAPT002 00088
***   09/24/85                                                  NAPT002 00089
***        ALWAYS CHANGE $$$$$$ AND ?????? TO SUBMITTER USER-ID NAPT002 00090
***                                                             NAPT002 00091
*** AUTHOR(S).                                                  NAPT002 00092
***                                                             NAPT002 00093
***          THIS WAS WRITTEN USING THE IKJEFF10 SOURCE FROM    NAPT002 00094
***          GENERAL DYNAMICS ON THE C. B. T. TAPE.             NAPT002 00095
***          CLARK MORRIS, NORTH AMERICAN PHILIPS LIGHTING      NAPT002 00096
***          SHARE CODE WEJ, 201-429-3607                       NAPT002 00097
***                                                             NAPT002 00098
*** END --- NORTH AMERICAN PHILIPS LIGHTING TSO MOD PROLOG ---- NAPT002 00099
R0       EQU   0                                                NAPT002 00100
R1       EQU   1                                                NAPT002 00101
R2       EQU   2                                                NAPT002 00102
R3       EQU   3                                                NAPT002 00103
R4       EQU   4                                                NAPT002 00104
R5       EQU   5                                                NAPT002 00105
R6       EQU   6                                                NAPT002 00106
R7       EQU   7                                                NAPT002 00107
R8       EQU   8                                                NAPT002 00108
R9       EQU   9                                                NAPT002 00109
R10      EQU   10                                               NAPT002 00110
R11      EQU   11                                               NAPT002 00111
R12      EQU   12                                               NAPT002 00112
R13      EQU   13                                               NAPT002 00113
R14      EQU   14                                               NAPT002 00114
R15      EQU   15                                               NAPT002 00115
         SPACE 2                                                NAPT002 00116
START    DS    0H                 START IT UP                   NAPT002 00117
         STM   R14,R12,12(R13)    SAVE REGISTERS                NAPT002 00118
         BALR  R10,0              LOAD R10                      NAPT002 00119
         USING *,R10              USE R10 AS BASE REG           NAPT002 00120
         LR    R9,R1              SAVE POINTER TO POINTER       NAPT002 00121
         XC    8(4,R13),8(R13)    TELL OS NO CURRENT SAVE AREA  NAPT002 00122
         SPACE 2                                                NAPT002 00123
         L     R9,0(R9)           SAVE POINTER TO SUBMIT DSECT  NAPT002 00124
         USING IEDSECTD,R9        SET R9 POINTER TO SUBMIT DATA NAPT002 00125
         L     R7,IESUBCTP        LOAD R7 WITH ADDR SUB POINTER NAPT002 00126
         USING IESUBCTD,R7        MAKE R7 POINTER TO 2ND DSECT  NAPT002 00127
         L     R8,IEMSGP          LOAD R8 WITH MESSAGE TO REPLY NAPT002 00128
         LTR   R8,R8              IS THERE A MESSAGE THERE?     NAPT002 00129
         BZ    CHKJOB             KEEP LOOKING                  NAPT002 00130
         TM    IEEXITWD,X'80'     IF REPEAT MESSAGE FLAG SET    NAPT002 00131
         BNO   FREEMSG               UNSET FLAG AND REPEAT      NAPT002 00132
         NI    IEEXITWD,X'7F'        THE MESSAGE                NAPT002 00133
         B     MSGSEND                                          NAPT002 00134
FREEMSG  DS    0H                                               NAPT002 00135
         FREEMAIN R,LV=WORKLTH,A=(8) FREE MESSAGE AREA          NAPT002 00136
         SLR   R8,R8              ZERO OUT MESSAGE POINTER      NAPT002 00137
         ST    R8,IEMSGP                                        NAPT002 00138
         L     R8,IEREPLYP        GET ADDR OF REPLY (IF ANY)    NAPT002 00139
         LTR   R8,R8              IF ZERO GO TO ABORT CYCLE     NAPT002 00140
         BZ    ABORT1                                           NAPT002 00141
         CLI   1(R8),X'04'        IF REPLY SIZE LESS THAN 2     NAPT002 00142
         BL    MORCHK               REPEAT THE MESSAGE          NAPT002 00143
         CLI   1(R8),X'05'        IF REPLY SIZE NOT = 3 OR ANS  NAPT002 00144
         BH    MORCHK               NOT YES AND NOT NO REPEAT   NAPT002 00145
         BE    CHKYES               THE MESSAGE                 NAPT002 00146
         CLC   =C'NO',2(R8)       IF ANSWER IS NO, GO TO ABORT  NAPT002 00147
         BE    ABORT1               CYCLE                       NAPT002 00148
         B     MORCHK                                           NAPT002 00149
CHKYES   DS    0H                                               NAPT002 00150
         CLC   =C'YES',2(R8)      IF ANSWER IS YES SUBMIT THE   NAPT002 00151
         BNE   MORCHK                JOB                        NAPT002 00152
         MVI   IEEXITWD,X'00'     SUPPRESS FURTHER CHECKING     NAPT002 00153
         L     R6,IECARDP         POINT TO JCL CARD             NAPT002 00154
         LTR   R6,R6               IF ZERO ADDRESS GO TO FINISH NAPT002 00155
         BZ    ABORT2                OF THE ABORT CYCLE         NAPT002 00156
         L     R3,IEUSRIDP       GET ADDR OF USER-ID IN R3      NAPT002 00157
         B     CHKCARD                                          NAPT002 00158
ABORT1   DS    0H                                               NAPT002 00159
         L     R6,IECARDP         POINT TO JCL CARD             NAPT002 00160
         LTR   R6,R6             IF CARD POINTER ZERO           NAPT002 00161
         BZ    ABORT2              GO TO ACTUAL ABORT           NAPT002 00162
         TM    IESTMTYP,IESJOB   IF JOB CARD AND NOT            NAPT002 00163
         BZ    ABORT1A              CONTINUATION FORCE DO NOT   NAPT002 00164
         TM    IESTMTYP,IESCONTN    FORCE JCL ERROR             NAPT002 00165
         BZ    ABORT2                                           NAPT002 00166
ABORT1A  DS    0H                                                       00167
         TM    IEEXITWD,X'40'    IF /* PURGE ALREADY SUBMITTED  NAPT002 00168
         BO    ABORT2              GO TO ACTUAL ABORT           NAPT002 00169
         OI    IEEXITWD,X'40'    SET EXIT WORD TO SHOW PURGE    NAPT002 00170
         MVC   1(7,R6),=C'*PURGE '  CARD SUBMITTED, SUBMIT IT   NAPT002 00171
         LA    R15,IERETURN         SET UP TO RETURN TO EXIT TO NAPT002 00172
         B     RETURN                ABORT SUMBIT               NAPT002 00173
CHKJOB   DS    0H                                               NAPT002 00174
         L     R6,IECARDP         POINT TO JCL CARD             NAPT002 00175
         LTR   R6,R6             IF CARD POINTER ZERO           NAPT002 00176
         BZ    ABORT2              GO TO FORCE BAD JCL          NAPT002 00177
         L     R3,IEUSRIDP       GET ADDR OF USER-ID IN R3      NAPT002 00178
         TM    IESTMTYP,IESCONTN  IS THIS A CONTINUATION?       NAPT002 00179
         BO    CHKCARD            ..YES, CHECK FOR MESSAGE CLAS NAPT002 00180
         TM    IESTMTYP,IESJOB    IS THIS A JOB CARD?           NAPT002 00181
         BO    CHKJOB1            ..YES, GO SEE IF VALID JOB NM NAPT002 00182
CHKCARD  DS    0H                                               NAPT002 00183
         LA    R11,62             GET INITIAL LENGTH FOR TRT    NAPT002 00184
         LA    R12,3(,R6)         POINT TO STARTING CHR FOR TRT NAPT002 00185
TRTLOOP  DS    0H                                               NAPT002 00186
         EX    R11,TRTINS         EXECUTE TRT FOR QUESTION MARK NAPT002 00187
         BZ    MORECARD           IF NO QUESTION MK OR $ EXIT   NAPT002 00188
*                                    WITH CHECK FOR MORE CARD   NAPT002 00189
         CLC   0(6,R1),=C'??????' IF SIX QUESTION MARKS OR 6    NAPT002 00190
         BE    CNGUSER              DOLLAR SIGNS GO TO CHNG USR NAPT002 00191
         CLC   0(6,R1),=C'$$$$$$'                               NAPT002 00192
         BE    CNGUSER                                          NAPT002 00193
         CLC   0(8,R1),=C'MSGCLASS' IF NOT MSGCLASS GO TO CHECK NAPT002 00194
         BNE   CHKSYSOT               SYSOUT                    NAPT002 00195
         CLI   9(R1),C'X'           IF MSGCLASS X OR Y GO TO    NAPT002 00196
         BE    CHKVALID               CHECK IF SUBMITTER VALID  NAPT002 00197
         CLI   9(R1),C'Y'                                       NAPT002 00198
         BE    CHKVALID                                         NAPT002 00199
BUMP11   DS    0H                                               NAPT002 00200
         LA    R1,11(,R1)         INCREMENT PAST MSGCLASS       NAPT002 00201
         B     CHKR1END                                         NAPT002 00202
CHKSYSOT DS    0H                                               NAPT002 00203
         CLC   0(7,R1),=C'SYSOUT=' IF NOT SYSOUT GO TO CHECK    NAPT002 00204
         BNE   BUMP1                  NEXT CHAR                 NAPT002 00205
         CLI   7(R1),C'X'          IF SYSOUT CLASS X OR Y GO TO NAPT002 00206
         BE    CHKVALID               CHECK IF SUBMITTER VALID  NAPT002 00207
         CLI   7(R1),C'Y'                                       NAPT002 00208
         BE    CHKVALID                                         NAPT002 00209
         CLI   8(R1),C'X'                                       NAPT002 00210
         BE    CHKVALID                                         NAPT002 00211
         CLI   8(R1),C'Y'                                       NAPT002 00212
         BE    CHKVALID                                         NAPT002 00213
BUMP8    DS    0H                                               NAPT002 00214
         LA    R1,08(,R1)         INCREMENT PAST SYSOUT         NAPT002 00215
         B     CHKR1END                                         NAPT002 00216
CNGUSER  DS    0H                                               NAPT002 00217
         MVC   0(6,R1),0(R3)      MOVE IN USER-ID               NAPT002 00218
         LA    R1,6(,R1)          BUMP R1                       NAPT002 00219
         B     CHKR1END                                         NAPT002 00220
BUMP1    DS    0H                                               NAPT002 00221
         LA    R1,1(,R1)                                        NAPT002 00222
CHKR1END DS    0H                                               NAPT002 00223
         LA    R11,65(,R6)        GET NEW SIZE FOR TRT AND IF   NAPT002 00224
         SR    R11,R1                NEGATIVE CARD IS DONE      NAPT002 00225
         BM    MORECARD                                         NAPT002 00226
         LR    R12,R1                                           NAPT002 00227
         B     TRTLOOP                                          NAPT002 00228
TRTINS   TRT   0(1,R12),SPCCHTBL                                NAPT002 00229
MORECARD DS    0H                                               NAPT002 00230
         MVI   0(R7),IETJOB+IETEXEC+IETDD+IETJES+IETCOMNT       NAPT002 00231
*               RETURN JOB, EXEC, DD, JES AND COMMENT CARDS     NAPT002 00232
         LA    R15,IECONTIN       RETURN CODE OF ZERO           NAPT002 00233
         B     RETURN             GET OUT OF HERE               NAPT002 00234
CHKJOB1  DS    0H                                               NAPT002 00235
         LA    R12,2(,R6)         POINT R12 TO JOBNAME          NAPT002 00236
         LR    R1,R12             GUARANTEE R1 GOOD             NAPT002 00237
         TRT   0(9,R12),SPACETBL  CHECK FOR LOC OF SPACE        NAPT002 00238
         BZ    INVALID               IF NONE BY COL 11, BAD JOB NAPT002 00239
*                                       NAME                    NAPT002 00240
         LR    R5,R1              GET SIZE OF JOB NAME          NAPT002 00241
         SR    R5,R12                                           NAPT002 00242
         CLC   0(6,R12),=C'??????' IF JOB NAME = 6 QUESTION     NAPT002 00243
         BE    SETUSER                MARKS OR $$$$$$ MOVE IN   NAPT002 00244
         CLC   0(6,R12),=C'$$$$$$'    USER-ID                   NAPT002 00245
         BNE   CHKJOB1A                                         NAPT002 00246
SETUSER  DS    0H                                               NAPT002 00247
         MVC   0(6,R12),0(R3)     MOVE IN USER-ID               NAPT002 00248
CHKJOB1A DS    0H                                               NAPT002 00249
         MVI   IEEXITWD,X'00'     SET EXITWORD TO ALLOW JOB     NAPT002 00250
         CH    R5,=H'8'           IF JOB NAME IS 8 CHRS OR LESS NAPT002 00251
         BE    CHKJOB2               THAN 6 CHRS CHECK IF OPER, NAPT002 00252
         CH    R5,=H'6'              SYS ENG, OR TECH SERVICES  NAPT002 00253
         BL    CHKJOB2               IS SUBMITTER               NAPT002 00254
         CLC   0(6,R12),0(R3)     IF JOB-NAME IS NOT THE SAME   NAPT002 00255
         BNE   CHKJOB2             AS THE USER-ID (OR THE 1ST   NAPT002 00256
*                                  6 CHARACTERS CHANGED TO THE  NAPT002 00257
*                                  USER ID) GO TO CHECK         NAPT002 00258
*                                  SUBMITTER                    NAPT002 00259
SETCHECK DS    0H                                               NAPT002 00260
         LA    R12,8(R12)         POINT R12 TO COL 11           NAPT002 00261
         LA    R11,55             SET SIZE FOR TRT              NAPT002 00262
         B     TRTLOOP            GO TO THE TRANSLATE AND TEST  NAPT002 00263
*                                   LOOP                        NAPT002 00264
*                                                               NAPT002 00265
CHKJOB2  DS    0H                                               NAPT002 00266
         MVI   IEEXITWD,X'01'     SET EXITWORD TO CHK PRIV JOB  NAPT002 00267
         CLC   0(2,R12),=C'FC'    IF TEST JOB AND SIZE IS 6 OR  NAPT002 00268
         BNE   CHKJOB3               7 CHARACTERS SET EXITWORD  NAPT002 00269
         CH    R5,=H'7'              BACK TO NO CHECKING.       NAPT002 00270
         BH    CHKJOB3               IF THE SEVENTH CHARACTER   NAPT002 00271
         CH    R5,=H'6'              IS BLANK, MOVE IN A $ TO   NAPT002 00272
         BL    CHKJOB3               THE SEVENTH CHARACTER      NAPT002 00273
         MVI   IEEXITWD,X'00'                                   NAPT002 00274
         BH    CHKJOB3                                          NAPT002 00275
         MVI   6(R12),C'$'                                      NAPT002 00276
CHKJOB3  DS    0H                                               NAPT002 00277
         CLC   0(6,R3),=C'FCBSH1' IF OPERATIONS 1ST SHIFT,      NAPT002 00278
         BE    CHKLSTHD              OR ANY OPERATOR CHECK FOR  NAPT002 00279
         CLC   0(5,R3),=C'FCBOP'     LISTHDR JOB AND            NAPT002 00280
         BNE   CHKJOB4               AUTOMATICALLY ALLOW IT TO  NAPT002 00281
CHKLSTHD DS    0H                    BE SUBMITTED               NAPT002 00282
         CLC   =C'LISTHDR ',0(R12)                              NAPT002 00283
         BNE   SETCHECK                                         NAPT002 00284
         MVI   IEEXITWD,X'00'                                   NAPT002 00285
         B     SETCHECK                                         NAPT002 00286
CHKJOB4  DS    0H            IF SYSTEMS ENGINEERING OR TECHINAL NAPT002 00287
         CLC   0(3,R3),=C'FCH'       SERVICES SET UP TO ALLOW   NAPT002 00288
         BE    SETCHECK              SUBMISSION OF ANY JOB      NAPT002 00289**2
         CLC   0(3,R3),=C'FCI'       EVEN IF IT HAS A MESSAGE   NAPT002 00290
         BE    SETCHECK              CLASS OF X OR Y            NAPT002 00291
         MVI   IEEXITWD,X'02'     SET EXITWORD TO CHK NON-PRIV  NAPT002 00292
         B     SETCHECK           AND GO TO CHECK THE JOB CARD  NAPT002 00293
CHKVALID DS    0H                                               NAPT002 00294
         CLI   IEEXITWD,X'00'     IF EXIT WORD NOT SET FOR      NAPT002 00295
         BE    BUMP8                 CHECKING GO CHK AFTER      NAPT002 00296
*                                    MSGCLASS                   NAPT002 00297
INVALID  DS    0H                                               NAPT002 00298
         CLI   IEEXITWD,X'01'     IF PRIVILEGED SUBMITTER CHK   NAPT002 00299
         BE    MORCHK                                           NAPT002 00300
         GETMAIN R,LV=WORKLTH     GET SOME STORAGE              NAPT002 00301
         LA    R8,0(,R1)          SET R8 TO POINT AT STORAGE    NAPT002 00302
         USING WORKAREA,R8        WORKAREA IS DSECT             NAPT002 00303
         MVC   WORKLGTH,=AL2(WORKLTH) STORE MESSAGE AREA LENGTH NAPT002 00304
*                                    IN WORKAREA                NAPT002 00305
         MVC   WORKMSG,MSGCONST   MOVE IN INVALID MSG           NAPT002 00306
         ST    R8,IEMSGP          STORE POINTER TO MESSAGE AWAY NAPT002 00307
         OI    IEEXITWD,X'80'     SET EXIT WORD TO SIGNAL       NAPT002 00308
*                                    REPEAT OF MESSAGE          NAPT002 00309
MSGSEND  DS    0H                                               NAPT002 00310
         LA    R15,IEMSG          SET RETURN CODE               NAPT002 00311
         B     RETURN             RETURN                        NAPT002 00312
MORCHK   DS    0H                                               NAPT002 00313
         GETMAIN R,LV=WORKLTH     GET SOME STORAGE              NAPT002 00314
         LA    R8,0(,R1)          SET R8 TO POINT AT STORAGE    NAPT002 00315
         USING WORKAREA,R8        WORKAREA IS DSECT             NAPT002 00316
         MVC   WORKLGTH,=AL2(WORKLTH) STORE MESSAGE AREA LENGTH NAPT002 00317
*                                    IN WORKAREA                NAPT002 00318
         MVC   WORKMSG,MSGCONSR   MOVE IN REQUEST FOR REPLY     NAPT002 00319
         DROP  R8                                               NAPT002 00320
         ST    R8,IEMSGP          STORE POINTER TO MESSAGE AWAY NAPT002 00321
         LA    R15,IEPROMPT       SET RETURN CODE TO REPLY      NAPT002 00322
         B     RETURN             RETURN                        NAPT002 00323
ABORT2   DS    0H                                               NAPT002 00324
         SLR   R15,R15            ZERO OUT EXITWORD             NAPT002 00325
         ST    R15,IEEXITWD                                     NAPT002 00326
         LA    R15,IEABORT                                      NAPT002 00327
         MVI   0(R7),IETJOB       RETURN ONLY JOB CARDS         NAPT002 00328
         B     RETURN                                           NAPT002 00329
RETURN   DS    0H                                               NAPT002 00330
         ST    R15,16(,R13)       STORE RETURN CODE             NAPT002 00331
         LM    R14,R12,12(R13)    RESTORE REGISTERS             NAPT002 00332
         BR    R14                RETURN                        NAPT002 00333
         EJECT                                                  NAPT002 00334
         DS    0F                                               NAPT002 00335
SPACETBL DS    0CL256             TABLE FOR TRT FOR SPACES      NAPT002 00336
         DC    64X'00'                                          NAPT002 00337
         DC    X'40'              SPACE POSITION                NAPT002 00338
         DC    191X'00'                                         NAPT002 00339
SPCCHTBL DS    0CL256             TABLE FOR TRT FOR QUESTION MK NAPT002 00340
         DC    91X'00'                                          NAPT002 00341
         DC    X'5B'              $ POSITION                    NAPT002 00342
         DC    19X'00'                                          NAPT002 00343
         DC    X'6F'              QUESTION MARK (?) POSITION    NAPT002 00344
         DC    100X'00'                                         NAPT002 00345
         DC    X'D4'              M POSITION                    NAPT002 00346
         DC    13X'00'                                          NAPT002 00347
         DC    X'E2'              S POSITION                    NAPT002 00348
         DC    30X'00'                                          NAPT002 00349
MSGCONST DC    C'*** JOB NOT SUBMITTED, IF MSGCLASS = X OR Y, JOBNAME M*00350
               UST BE 6 OR 7  BYTES WITH THE FIRST 6 BEING YOUR USER-ID*00351
                OR $$$$$$ '                                     NAPT002 00352
MSGCONSR DC    C'+++ DO YOU WISH TO SUBMIT A JOB WITH MESSAGE CLASS OR *00353**2
               SYSOUT OF X OR Y THAT IS NOT A USER-ID PLUS 1 CH, REPLY *00354
               YES OR NO  '                                     NAPT002 00355
         EJECT                                                  NAPT002 00356
         LTORG                                                  NAPT002 00357
         EJECT                                                  NAPT002 00358
WORKAREA DSECT                                                  NAPT002 00359
WORKLGTH DS    CL2                                              NAPT002 00360
WORKMSG  DC    C'*** JOB NOT SUBMITTED, IF MSGCLASS = X OR Y, JOBNAME M*00361
               UST BE 6 OR 7  BYTES WITH THE FIRST 6 BEING YOUR USER-ID*00362
                OR $$$$$$ '                                     NAPT002 00363
WORKLTH  EQU   *-WORKLGTH                                       NAPT002 00364
         IKJEFFIE IETYPE=SUBMIT   SUBMIT DSECT                  NAPT002 00365
         CSECT ,                                                NAPT002 00366
         DC    C'NAPT002 10/19/85'                              NAPT002 00367**2
         END   ,                                                NAPT002 00368
