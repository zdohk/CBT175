++ USERMOD (NAPJ007) .
++ VER (Z038) FMID(HJE2221).
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT13),DISP=SHR
//SYSIN  DD  SYS1.JES2SRC(J2XIT13),DISP=SHR
//STEP2    EXEC PGM=IEWL,PARM='RENT'
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT13),DISP=SHR
//SYSLMOD  DD  DSN=SYS1.JES2LIB,DISP=SHR
//SYSLIN  DD  *
  INCLUDE SYSPUNCH(J2XIT13)
  NAME J2XIT13(R)
++SRC (J2XIT13) DISTLIB(JES2SRC).
*          DATA SET J2XIT13    AT LEVEL 012 AS OF 07/09/89
EXIT     TITLE 'PHILIPS LIGHTING NJE/TSO/E SYSOUT VALIDATION FOR  , CON*00001**2
               EMC2/TAO/PROFS INTERFACE'                                00002**5
         SPACE 5                                                        00003
         COPY  $HASPGBL                                                 00004
         PRINT GEN                                                      00005**3
J2XIT13  $MODULE TITLE='PHILIPS LIGHTING (WEJ) HASP EXIT ROUTINES',    X00006**2
               RPL,                GENERATE HASP RPL    DSECT          X00007**5
               $BUFFER,            GENERATE HASP BUFFER DSECT          X00008**5
               $CAT,               GENERATE HASP CAT DSECT             X00009
               $HCT,               GENERATE HASP HCT DSECT             X00010
               $JCT,               GENERATE HASP JCT DSECT             X00011
               $PADDR,             GENERATE HASP JCT DSECT             X00012**3
               $PDDB,              GENERATE HASP PDDB DSECT            X00013**8
               $JQE,               GENERATE HASP JQE DSECT             X00014
               $MIT,               GENERATE HASP MIT DSECT             X00015
               $PCE,               GENERATE HASP PCE DSECT             X00016
               $NHD,               GENERATE HASP NDJ AND NJH DSECTS    X00017**8
               $HASPEQU                                                 00018**2
         TITLE 'JES2 EXIT 6 INTERNAL TEXT EXIT'                         00019
*********************************************************************** 00020
*                                                                     * 00021
*        ENVIRONMENT   MVS SP 2.2.0                                   * 00022**8
*                      JES2 FOR SP 2.2.0 (XA)                         * 00023**8
*                                                                     * 00024
J2XIT13  AMODE 31                                                       00025**2
J2XIT13  RMODE 24                                                       00026**2
         EJECT                                                          00027
*********************************************************************** 00028
*                                                                     * 00029
*        REGISTER CONTENTS ON ENTRY TO THIS EXIT ROUTINE:             * 00030
*                                                                     * 00031
*        R0 - N/A                                                     * 00032**8
*        R1       - ADDRESS OF 2-WORD PARAMETER LIST                  * 00033
*           WORD 1 (+0)  ADDRESS OF THE NETWORK JOB HEADER            * 00034**8
*           WORD 2 (+4)  ADDRESS OF THE NETWORK DATA SET HEADER       * 00035**8
*           WORD 3 (+8)  ADDRESS OF THE PDDB                          * 00036**8
*           WORD 4 (+12) ADDRESS OF THE SYSID OF LOGGED ON USER       * 00037**8
*           WORD 5 (+16) ADDRESS OF THE 70 BYTE DELETE MESSAGE        * 00038**8
*        R11      - ADDRESS OF THE HCT                                * 00039
*        R13      - ADDRESS OF AN OS-STYLE SAVE AREA                  * 00040
*        R14      - RETURN ADDRESS                                    * 00041
*        R15      - ENTRY ADDRESS                                     * 00042
*                                                                     * 00043
*********************************************************************** 00044
         SPACE 4                                                        00045
*********************************************************************** 00046
*                                                                     * 00047
*        REGISTER USAGE WITH THIS EXIT:                               * 00048
*                                                                     * 00049
*        R0,R1    - PARM/WORK REGISTERS                               * 00050
*        R3       - ADDR OF THE NJH                                   * 00051**8
*        R4       - ADDR OF THE NDH                                   * 00052**8
*        R5       - ADDR OF THE PDDB                                  * 00053**8
*        R6       - OPEN                                              * 00054**8
*        R7       - OPEN                                              * 00055**8
*        R8       - OPEN                                           WEJ* 00056**8
*        R9       - OPEN                                           WEJ* 00057**8
*        R10      - OPEN                                           WEJ* 00058**8
*        R11      - ADDRESS OF THE HCT                                * 00059
*        R12      - BASE REGISTER #1                                  * 00060
*        R13      - WORKAREA POINTER                                  * 00061
*        R14      - RETURN ADDRESS                                    * 00062
*        R15      - RETURN CONDITION CODE                             * 00063
*                                                                     * 00064
*********************************************************************** 00065
         EJECT                                                          00066
*********************************************************************** 00067
*                                                                     * 00068
*        ACTUAL JES2 EXIT ENTRY POINT                                 * 00069
*                                                                     * 00070
*********************************************************************** 00071
J2XIT13E $ENTRY  BASE=R12                                               00072**2
         $SAVE                                                          00073**2
         LR    R12,R15                                                  00074**8
         LM    R3,R5,0(R1)             GET POINTERS TO NJH,NDH,PDDB     00075**8
         USING NJH,R3                                                   00076**8
         USING NDH,R4                                                   00077**8
         USING PDB,R5                                                   00078*10
         CLI   PDBCLASS,C'A'              IS SYSOUT CLASS=A             00079**8
         BNE   EXIT                       NO, GET OUT                   00080**8
         TM    NDHGFLG2,NDHGF2PU          IS THIS PUNCH                 00081**8
         BZ    EXIT                       NO, GET OUT                   00082**8
         CLC   PDBDRMT,=H'0'              IS DEST REMOTE ZERO           00083**8
         BNE   EXIT                       NO, GET OUT                   00084**8
         CLC   NDHGXWTR,=CL8' '           IS EXTERNAL WRITER FLD BLANK  00085*11
         BE    EXIT                       YES, GET OUT                  00086*11
         CLC   NDHGXWTR,=XL8'00'          IS EXTERNAL WTR FLD LOW-VALUE 00087*11
         BE    EXIT                       YES, GET OUT                  00088*11
         CLC   NDHGXWTR(2),=C'FC'         ARE 1ST 2 CHARS OF EXTERNAL   00089*12
*                                           WRITER FIELD FC (TSO USER)  00090*12
         BNE   CHGCLASS                   NO , USE IT                   00091*12
         CLC   NDHGXWTR+6(2),=C'  '       ARE LAST 2 CHARS OF EXTERNAL  00092*12
*                                           WRITER FLD BLANK (CONFIRM   00093*12
*                                           TSO USER ID)                00094*12
         BE    EXIT                       YES, EXIT                     00095*12
CHGCLASS DS    0H                                                       00096*12
         MVI   PDBCLASS,C'N'              CHANGE CLASS TO N             00097**8
EXIT     DS    0H                                                       00098**8
         $RETURN RC=0                                                   00099**8
         $MODEND                                                        00100**2
         END                                                            00101**2
