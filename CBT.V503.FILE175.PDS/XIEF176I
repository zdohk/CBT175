*          DATA SET XIEF176ICM AT LEVEL 004 AS OF 05/13/90
*          DATA SET XIEF176ICM AT LEVEL 003 AS OF 03/29/90              00001**1
XIEF176I TITLE 'LIGHTING MESSAGE HANDLING EXIT            '             00002**1
*/* LIB:  N/A                                                           00003**1
*/* GDE:  GC28-1147 MVS/XA SPL USER EXITS                               00004**1
*/* DOC:  THIS EXIT IS TAKEN FROM THE CANCWAIT SAMPLE ON PAGE 51        00005**1
*/*       ON THE JOB LOG WITH STEP TERMINATION STATUS.                  00006**1
*********************************************************************** 00007**1
*                                                                     * 00008**1
*             MODULE NAME = XIEF176I                                  * 00009**1
*                                                                     * 00010**1
*             DESCRIPTIVE NAME =  COMMUNICATIONS TASK USER EXIT FOR   * 00011**1
*                MESSAGE IEF176I                                      * 00012**1
*                                                                     * 00013**1
*             PHILIPS LIGHTING 1990, THIS IS IN THE PUBLIC DOMAIN AND * 00014**1
*             IS IN USE AT PHILIPS LIGHTING.  PLEASE FEEL FREE TO USE * 00015**1
*             AT YOUR OWN RISK.  NO WARRANTY EXPRESS OR IMPLIED IS    * 00016**1
*             GIVEN                                                   * 00017**1
*                                                                     * 00018**1
*             FUNCTION =                                              * 00019**1
*                ISSUE 'P WTRLAUTO' IF WRITER WATING FOR WORK IS      * 00020**1
*                CLASS L                                              * 00021**1
*              NOTES =                                                * 00022**1
*                                                                     * 00023**1
*                 DEPENDENCIES = CHARACTER SET IS EBCDIC.  REASSEMBLE * 00024**1
*                    IF A DIFFERENT CHARACTER SET IS NEEDED.          * 00025**1
*                                                                     * 00026**1
*                 RESTRICTIONS = NONE                                 * 00027**1
*                                                                     * 00028**1
*                 REGISTER CONVENTIONS = STANDARD CONVENTIONS.        * 00029**1
*                    REGISTERS 0 TO 1  = WORK REGISTERS               * 00030**1
*                    REGISTERS 2 TO 11 = SEE REGISTER EQUATES BELOW   * 00031**1
*                    REGISTER  12      = ADDRESSABILITY TO XIEF176I   * 00032**1
*                                        CSECT                        * 00033**1
*                    REGISTER  13      = ADDRESSIBILITY TO DATA DSECT * 00034**1
*                    REGISTERS 14,15   = WORK REGISTERS               * 00035**1
*                                                                     * 00036**1
*                PATCH LABEL = PATCH (UNUSED AND INTIALIZED TO        * 00037**1
*                   BINARY ZEROES)                                    * 00038**1
*                                                                     * 00039**1
*             MODULE TYPE = PROCEDURE                                 * 00040**1
*                                                                     * 00041**1
*                PROCESSOR = ASM                                      * 00042**1
*                                                                     * 00043**1
*                ATTRIBUTES = KEY 0, REENTRANT,                       * 00044**1
*                   SUPERVISOR STATE, ENABLED, NO LOCKS HELD          * 00045**1
*                                                                     * 00046**1
*             ENTRY POINTS = XIEF176I (ONLY ENTRY POINT)              * 00047**1
*                                                                     * 00048**1
*                LINKAGE =                                            * 00049**1
*                                                                     * 00050**1
*                                                                     * 00051**1
*             INPUT = REG1 POINTS TO THE ADDRESS OF THE               * 00052**1
*                     COMMUNICATIONS TASK EXIT PARAMETER LIST (CTXT)  * 00053**1
*                     REGS 0 AND 2-12 IRRELEVANT                        00054**1
*                     REG 13, STANDARD SAVE AREA                        00055**1
*                     REG 14, RETURN ADDRESS                            00056**1
*                     REG 15, ENTRY POINT ADDRESS                       00057**1
*                                                                     * 00058**1
*             OUTPUT = NONE                                           * 00059**1
*                                                                     * 00060**1
*             EXIT - NORMAL = AT PROGRAM END VIA BRANCH REGISTER 14   * 00061**1
*                                                                     * 00062**1
*                OUTPUT = NONE                                        * 00063**1
*                                                                     * 00064**1
*                RETURN CODE = ZERO                                   * 00065**1
*                                                                     * 00066**1
*             EXIT - ERROR = NONE                                     * 00067**1
*                                                                     * 00068**1
*                OUTPUT = NONE                                        * 00069**1
*                                                                     * 00070**1
*                RETURN CODE = ZERO                                   * 00071**1
*                                                                     * 00072**1
*             EXTERNAL REFERENCES =                                   * 00073**1
*                                                                     * 00074**1
*                ROUTINES = NONE                                      * 00075**1
*                                                                     * 00076**1
*                DATA AREAS = NONE                                    * 00077**1
*                                                                     * 00078**1
*                CONTROL BLOCKS = NONE                                * 00079**1
*                                                                     * 00080**1
*             TABLES = NONE                                           * 00081**1
*                                                                     * 00082**1
*             MACROS = SYS1.MACLIB FREEMAIN, GETMAIN, IEZBITS,        * 00083**1
*                          IEZMGCR, PANAEXIT, RETURN, SAVE            * 00084**1
*                      SYS1.AMODGEN IEZVX100, MGCR                    * 00085**1
*                                                                     * 00086**1
*             CHANGE ACTIVITY = NONE                                  * 00087**1
*                                                                     * 00088**1
*                                                                     * 00089**1
*             ABEND CODES = NONE                                      * 00090**1
*                                                                     * 00091**1
*********************************************************************** 00092**1
         EJECT                                                          00093**1
XIEF176I CSECT ,                                                        00094**1
XIEF176I AMODE 31                                                       00095**1
XIEF176I RMODE ANY                                                      00096**1
*                                                                       00097**1
* CONSTANT ADDRESS EQAUTES                                              00098**1
*                                                                       00099**1
PSAAOLD  EQU    X'224',4    ADDRESS IN PSA OF CURRENT ASCB              00100**1
ASCBJBNI EQU    X'AC',4     ADDRESS IN JOBNAME IN ASCB                  00101**1
CHSTEP   EQU    X'38',8     ADDRESS IN STEP NAME IN CSCB                00102**1
CHPROCSN EQU    X'18',8     ADDRESS IN PROC STEP NAME IN CSCB           00103**1
*                                                                       00104**1
* CONSTANT EQUATES                                                      00105**1
*                                                                       00106**1
K0       EQU   00                  NON-RELOCATABLE CONSTANT 00          00107**1
K1       EQU   01                  NON-RELOCATABLE CONSTANT 01          00108**1
K2       EQU   02                  NON-RELOCATABLE CONSTANT 02          00109**1
K3       EQU   03                  NON-RELOCATABLE CONSTANT 03          00110**1
K4       EQU   04                  NON-RELOCATABLE CONSTANT 04          00111**1
K5       EQU   05                  NON-RELOCATABLE CONSTANT 05          00112**1
K6       EQU   06                  NON-RELOCATABLE CONSTANT 06          00113**1
K7       EQU   07                  NON-RELOCATABLE CONSTANT 07          00114**1
K8       EQU   08                  NON-RELOCATABLE CONSTANT 08          00115**1
K9       EQU   09                  NON-RELOCATABLE CONSTANT 09          00116**1
KA       EQU   10                  NON-RELOCATABLE CONSTANT 10          00117**1
KB       EQU   11                  NON-RELOCATABLE CONSTANT 11          00118**1
KC       EQU   12                  NON-RELOCATABLE CONSTANT 12          00119**1
KD       EQU   13                  NON-RELOCATABLE CONSTANT 13          00120**1
KE       EQU   14                  NON-RELOCATABLE CONSTANT 14          00121**1
KF       EQU   15                  NON-RELOCATABLE CONSTANT 15          00122**1
*                                                                       00123**1
* CONSTANT EQUATES                                                      00124**1
*                                                                       00125**1
SPINPRVT EQU   230                 USE SUBPOOL 230 FOR GETMAIN          00126**1
SUBPOOL  EQU   252                 USE SUBPOOL 252 FOR GETMAIN          00127**1
CK       EQU   C'K'                CONSTANT CHAR 'K'                    00128**1
CASTR    EQU   C'*'                CONSTANT CHAR '*'                    00129**1
C0       EQU   C'0'                CONSTANT CHAR '0'                    00130**1
         IEZBITS                                                        00131**1
*                                                                       00132**1
* REGISTER EQUATES                                                      00133**1
*                                                                       00134**1
R0       EQU   00                  REG  0 - WORK REG                    00135**1
R1       EQU   01                  REG  1 - WORK/PARMETER REG           00136**1
R2       EQU   02                  REG  2 - WORK REG                    00137**1
R3       EQU   03                  REG  3 - POINTER TO THE CTXT         00138**1
R4       EQU   04                  REG  4 - PTR TO THE MSSG ATTRIBUTES  00139**1
R5       EQU   05                  REG  5 - PTR TO THE MESSAGE AREA     00140**1
R6       EQU   06                  REG  6 - POINTER TO THE REPLY        00141**1
R7       EQU   07                  REG  7 - PTR TO MSSG ROUTING AREA    00142**1
R8       EQU   08                  REG  8 - USED BY EXECUTED MOVE       00143**1
R9       EQU   09                  REG  9 - UNUSED                      00144**1
R10      EQU   10                  REG 10 - UNUSED                      00145**1
R11      EQU   11                  REG 11 - UNUSED                      00146**1
R12      EQU   12                  REG 12 - CSECT BASE REG              00147**1
R13      EQU   13                  REG 13 - DATA DSECT BASE REG         00148**1
R14      EQU   14                  REG 14 - LINKAGE REGISTER            00149**1
R15      EQU   15                  REG 15 - WORK REG                    00150**1
         EJECT                                                          00151**1
         SAVE  (14,12)                                                  00152**1
         USING XIEF176I,R12        SET UP CSECT ADDRESSABILITY          00153**1
         LR    R12,R15             LOAD BASE REGISTER                   00154**1
         L     R3,0(,R1)           LOAD ADDRESS OF CTXT                 00155**1
         USING CTXT,R3                                                  00156**1
         L     R4,CTXTTXPJ         POINT TO THE MESSAGE ATTRIBUTES      00157**1
         USING CTXTATTR,R4                                              00158**1
         LA    R5,CTXTTMSG         POINT TO THE MESSAGE AREA            00159**1
         USING MSGTEXT,R5                                               00160**1
         L     R7,CTXTRCP          POINT TO MESSAGE ROUTING AREA        00161**1
         USING CTXTROUT,R7                                              00162**1
         CLC   WAITRESN,CLASLCON   IS THE REASON CLASS=L (SYSLOG)       00163**1
         BNE   EXIT                NO, EXIT                             00164**1
*********************************************************************** 00165**1
*                                                                     * 00166**1
*        DYNAMIC STORAGE FOR THIS MODULE IS BEING OBTAINED BELOW THE  * 00167**1
*        16-MEG LINE BECAUSE SVC 34 REQUIRES THE MGCR PARAMETER LIST  * 00168**1
*        TO BE IN 24-BIT ADDRESSABLE STORAGE                          * 00169**1
*                                                                     * 00170**1
*********************************************************************** 00171**1
         GETMAIN RU,LV=DATAEND,SP=SPINPRVT,LOC=BELOW OBTAIN DYNAMIC     00172**1
*                                                    STORAGE            00173**1
         USING DATAAREA,R1                                              00174**1
         ST    R13,SAVEPR13     SAVE THE SAVE AREA POINTER IN THE NEW   00175**1
         ST    R1,8(,R13)           AREA AND R1 IN THE PRIOR ONE        00176**1
         LR    R13,R1 POINT R13 TO THE DATA AREA                        00177**1
         DROP  R1                                                       00178**1
         USING DATAAREA,R13                                             00179**1
         XC    MGCRPL(MGCRLTH),MGCRPL  CLEAR OUT THE REPLY AREA         00180**1
         XC    CTXTROUT(2),CTXTROUT    CLEAR OUT THE ROUT CODE          00181**1
         MVC   MGCRLGTH,STOPCONL       MOVE IN COMMAND LENGHT           00182**1
         MVC   COMMAND,STOPCON         MOVE IN THE STOP COMMAND         00183**1
         SLR   R0,R0                                                    00184**1
         MGCR  MGCRPL                  ISSUE THE MESSAGE                00185**1
         MVC   MGCRLGTH,STRTCONL       MOVE IN COMMAND LENGHT           00186**4
         MVC   COMMAND(L'STRTCON),STRTCON  MOVE IN 'S P,M=PHUT1113'     00187**4
*                                      SUBMIT JOB TO TELL UCC7 TO       00188**4
*                                      SUBMIT PRINT SYSLOG JOB          00189**4
         SLR   R0,R0                                                    00190**4
         MGCR  MGCRPL                  ISSUE THE MESSAGE                00191**4
         LR    R8,R13                  RESTORE THE SAVE AREA POINTER    00192**1
         L     R13,SAVEPR13                                             00193**1
         FREEMAIN RU,LV=DATAEND,A=(R8),SP=SPINPRVT  FREE THE DATA AREA  00194**1
         DROP  R13                                                      00195**1
         OI    CTXTRFB1,CTXTRCRC       REQUEST TO CHANGE ROUT           00196**1
EXIT     DS    0H                                                       00197**1
         RETURN (14,12)                EXIT, NOTE RETURN CODE NOT GIVEN 00198**1
IEF176I  DC    CL7'IEF176I'                                             00199**1
STOPCONL DC    AL1(MGCRTEXT-MGCRPL+L'STOPCON)                           00200**1
STOPCON  DC    C'P WTRLAUTO'                                            00201**1
STRTCONL DC    AL1(MGCRTEXT-MGCRPL+L'STRTCON)                           00202**4
STRTCON  DC    C'S P,M=PHUT1113'                                        00203**4
CLASLCON DC    C'CLASS=L'                                               00204**3
DATAAREA DSECT                                                          00205**1
         DS    0D                                                       00206**1
SAVEAREA DS    0CL72                                                    00207**1
SAVEPL1  DS    F                                                        00208**1
SAVEPR13 DS    F                                                        00209**1
SAVENX13 DS    F                                                        00210**1
SAVE1412 DS    15F                                                      00211**1
         DS    0F                                                       00212**1
MGCR     IEZMGCR DSECT=NO                                               00213**1
         ORG   MGCRTEXT                                                 00214**1
COMMAND  DS    CL(L'STOPCON)                                            00215**2
         ORG                                                            00216**1
DATAEND  EQU   *-DATAAREA                                               00217**1
MSGTEXT  DSECT                                                          00218**1
MSGID    DS    CL8                                                      00219**1
JOBDATA  DS    CL8                                                      00220**1
         ORG   MSGTEXT                                                  00221**1
         DS    CL34                                                     00222**1
WAITRESN DS    CL(L'CLASLCON)                                           00223**2
         IEZVX100                                                       00224**1
         END                                                            00225**1
