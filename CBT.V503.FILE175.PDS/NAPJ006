++ USERMOD (NAPJ006) .
++ VER (Z038) FMID(HJE2221).
++JCLIN.
//STEP1    EXEC PGM=IEV90
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT09),DISP=SHR
//SYSIN  DD  SYS1.JES2SRC(J2XIT09),DISP=SHR
//STEP2    EXEC PGM=IEWL,PARM='RENT'
//SYSPUNCH DD  DSN=&&PUNCH(J2XIT09),DISP=SHR
//SYSLMOD  DD  DSN=SYS1.JES2LPA,DISP=SHR
//SYSLIN  DD  *
  INCLUDE SYSPUNCH(J2XIT09)
  NAME J2XIT09(R)
++SRC (J2XIT09) DISTLIB(JES2SRC).
*          DATA SET J2XIT09    AT LEVEL 009 AS OF 12/10/89
J2XIT09  TITLE 'WFB - EXIT 009 - JOB OUTPUT OVERFLOW'                   00001**5
*********************************************************************** 00002
*                                                                     * 00003
* MODULE NAME: J2XIT09                                                * 00004**5
*                                                                     * 00005
* ENVIRONMENT: USER ADDRESS SPACE (MVS CONVENTIONS MUST BE USED)      * 00006
*                                                                     * 00007
* DESCRIPTIVE NAME: JES2 EXIT 009 - LINE EXCESSION OVERFLOW HANDLING  * 00008**5
*           (ORIGINALLY USED FOR TSO/E TRANSMIT LINE LIMITATION)      * 00009**5
* AUTHOR: TOM LITNEY                                                  * 00010
*         WELLS FARGO BANK, N.A. (WFB)                                * 00011
*         525 MARKET STREET, MAC 0103-141                             * 00012
*         SAN FRANCISCO, CALIFORNIA  94163                            * 00013
*         (415) 396-7883                                              * 00014
* CHANGED BY : CLARK MORRIS                                           * 00015**5
*         PHILIPS LIGHTING, DEPT 5069                                 * 00016**5
*         PHILIPS SQUARE, CN6800                                      * 00017**5
*         SOMERSET, NEW JERSEY 08875-6800                             * 00018**5
*         (201) 563-3575                                              * 00019**5
*                                                                     * 00020
* FMID: HJE2226 (MVS/SP-JES2 2.2.0)                               TL1 * 00021**5
*                                                                     * 00022
* DATE WRITTEN: JANUARY 1985                                          * 00023
*                                                                     * 00024
* CHANGE HISTORY:                                                     * 00025
*  MM/YY  WHO          WHAT                                       ID# * 00026
*  05/86  TOM LITNEY   CHANGES FOR JES2 SP 2.1.5                  TL1 * 00027
*  12/89  CLARK MORRIS CHANGED FUNCTION AND INSTALLED ON JES2 XA  CFM * 00028**5
*                      2.2.0                                          * 00029**5
*                                                                     * 00030**5
* PURPOSE: THE LINES EXCESSION ROUTINE IS CHANGED SO THAT PRODUCTION  * 00031**4
*          JOBS ARE CANCELLED IF THEY EXCEED 2.5 MILLION LINES OR THE * 00032**5
*          LINE LIMIT IN THE JOBPARM CARD (PRODUCTION JOBS ARE THOSE  * 00033**5
*          SUBMITTED BY UCC7 OR A SPECIAL SUBMISSION STARTED TASK     * 00034**5
*          CALLED COMMAND).  ALL OTHER JOBS AND TSO SIGN-ONS WILL BE  * 00035**5
*          CANCELLED IF THE /*JOBPARM LINE LIMIT IS EXCEEDED OR, IF   * 00036**5
*          NO JOBPARM LINE LIMIT IS PRESENT, THE INITIALIZATION DECK  * 00037**5
*          LINE LIMIT IS EXCEEDED.  STARTED TASKS WILL BE EXEMPT FROM * 00038**7
*          THIS CHECKING AND THE HASP375 MESSAGE WILL BE SUPPRESSED   * 00039**5
*          FOR THE STARTED TASK CADSDISP (DISPATCH, A REPORT          * 00040**5
*          MANAGEMENT SYSTEM) UNTIL 2.5 MILLION LINES ARE EXCEEDED.   * 00041**5
*          EXIT 10 HAS BEEN INSTALLED TO CHANGE THE DESCRIPTOR CODE   * 00042**5
*          OF THE HASP375 MESSAGE TO EVENTUAL ACTION SO THAT IT IS    * 00043**5
*          HIGHLIGHTED AND REMAINS ON THE SCREEN                      * 00044**5
*                                                                     * 00045**5
* ENTRY POINT: J2XIT09                                                * 00046**5
*                                                                     * 00047
* REGISTERS ON INPUT:                                                 * 00048
*     R0: N/A                                                         * 00049
*     R1: ADDRESS OF 7-WORD PARAMETER LIST WITH THE FOLLOWING LAYOUT: * 00050
*         WORD 1 (+0)  JCTLINES OR JCTPUNCH VALUE                     * 00051
*         WORD 2 (+4)  JCTPAGES VALUE                                 * 00052
*         WORD 3 (+8)  JCTMXOUT VALUE                                 * 00053
*         WORD 4 (+12) USERS INCREMENT FOR RECORDS                    * 00054
*         WORD 5 (+16) USERS INCREMENT FOR PAGES                      * 00055
*         WORD 6 (+20) USERS INCREMENT FOR BYTES                      * 00056
*         WORD 7 (+24) OUTPUT OVERFLOW FLAG                           * 00057
*              BIT 0   =0  CARDS HAVE NOT EXCEEDED THE LIMIT          * 00058
*                      =1  CARDS HAVE EXCEEDED THE ESTIMATE           * 00059
*              BIT 1   =0  LINES HAVE NOT EXCEEDED THE LIMIT          * 00060
*                      =1  LINES HAVE EXCEEDED THE ESTIMATE           * 00061
*              BIT 2   =0  PAGES HAVE NOT EXCEEDED THE LIMIT          * 00062
*                      =1  PAGES HAVE EXCEEDED THE ESTIMATE           * 00063
*              BIT 3   =0  BYTES HAVE NOT EXCEEDED THE LIMIT          * 00064
*                      =1  BYTES HAVE EXCEEDED THE ESTIMATE           * 00065
*              BIT 4-31    NOT APPLICABLE                             * 00066
*     R2-R6: N/A                                                      * 00067
*     R7 : ADDRESS OF THE JCT                                         * 00068
*     R8-R10: N/A                                                     * 00069
*     R11: ADDRESS OF THE SSVT                                        * 00070
*     R12: N/A                                                        * 00071
*     R13: OS-STYLE 18-WORD SAVE AREA                                 * 00072
*     R14: RETURN ADDRESS                                             * 00073
*     R15: ENTRY ADDRESS                                              * 00074
*                                                                     * 00075
* REGISTERS ON RETURN:                                                * 00076
*     R0 : IF THE RETURN CODE IN R15 IS 8 THEN:                       * 00077
*              BIT 0   =0 USE INSTALLATION CURRENT EXCESSIVE          * 00078
*                         OUTPUT OPTION VALUE                         * 00079
*                      =1 USE VALUE SPECIFIED IN BITS 24-31 AS        * 00080
*                         EXCESSIVE OUTPUT VALUE                      * 00081
*              BIT 1   =0 USE OUTPUT OVERFLOW INCREMENT AS SPECIFIED  * 00082
*                         ON ESTLNCT, ESTPUN, ESTPAGE, OR ESTBYTE.    * 00083
*                      =1 USE THE OUTPUT OVERFLOW INCREMENT AS        * 00084
*                         SUPPLIED BY THE EXIT ROUTINE IN THE         * 00085
*                         PARAMETER LIST                              * 00086
*              BIT 2   =0 SEND DEFAULT ERROR MESSAGE ($HASP375) TO    * 00087
*                         THE OPERATOR                                * 00088
*                      =1 SUPPRESS DEFAULT MESSAGE                    * 00089
*              BIT 3-23  N/A                                          * 00090
*              BIT 24-31 =0 CONTINUE PROCESSING                       * 00091
*                        =1 ABEND (722) WITHOUT A DUMP                * 00092
*                        =2 ABEND (722) WITH A DUMP                   * 00093
*     R1-R14: N/A                                                     * 00094
*     R15 : A RETURN CODE                                             * 00095
*           =0 PERFORM ANY ADDITIONAL EXITS ASSOCIATED WITH THIS EXIT.* 00096
*              PERFORM STANDARD JOB OUTPUT OVERFLOW PROCESSING BASED  * 00097
*              ON THE INT= AND OPT= KEYWORDS ON THE ESTLNCT, ESTPUN,  * 00098
*              ESTPAGE, AND ESTBYTE INITIALIZATIN PARAMETERS          * 00099
*           =4 DO NOT PERFORM ANY ADDITIONAL EXITS.                   * 00100
*              PERFORM STANDARD JOB OUTPUT OVERFLOW PROCESSING BASED  * 00101
*              ON THE INT= AND OPT= KEYWORDS ON THE ESTLNCT, ESTPUN,  * 00102
*              ESTPAGE, AND ESTBYTE INITIALIZATIN PARAMETERS          * 00103
*           =8 TAKE ACTION BASED ON RETURN PROCESSING FLAGS IN R0.    * 00104
*              SEE R0 ABOVE.                                          * 00105
*                                                                     * 00106
*********************************************************************** 00107
         EJECT                                                          00108
         COPY $HASPGBL                 COPY HASP GLOBALS                00109
         EJECT                                                          00110
J2XIT09  AMODE 31                                                       00111**4
J2XIT09  RMODE 24                                                       00112**4
J2XIT09  $MODULE                                                       C00113**4
               RPL,                    REQ'D BY $BUFFER                C00114
               $HASPEQU,               REQ'D FOR REG CONVENTIONS       C00115
               $MIT,                   REQ'D FOR EXIT MASK             C00116
               $BUFFER,                REQ'D FOR JCT                   C00117
               $JCT                    REQ'D TO GET JOBNAME, ETC.       00118
         EJECT                                                          00119
J2XIT09E $ENTRY BASE=R12               EXIT ROUTINE ENTRY POINT         00120**4
         SAVE  (14,12)                 SAVE CALLERS REGISTERS           00121**2
         LR    R12,R15                 LOAD LOCAL BASE REGISTER         00122
         SLR   R15,R15                 SET DEFAULT RETURN CODE          00123
         EJECT                                                          00124
*********************************************************************** 00125
*  SET UP ADDRESSIBILITY AND TEST FOR TSO USER AND PUNCH OVERFLOW. IF * 00126
*  NO RETURN.  IF YES, IT MUST BE TSO/E TRANSMIT UP PUNCH LIMIT, SET  * 00127
*  FLAGS, AND RETURN.                                                 * 00128
*********************************************************************** 00129
         USING JCT,R7                  ESTABLISH ADDRESSIBILITY     TL1 00130
         TM    24(R1),B'01000000'      LINES EXCEED THE LIMIT??         00131**2
         BZ    RETURN                  NO, RETURN                       00132
         CLI   JCTJOBID,C'S'           STARTED TASK???                  00133**2
         BNE   CHKJOB                  NO, CHECK FOR PRODUCTION JOB     00134**5
         CLC   JCTJNAME,=C'CADSDISP'   IS THIS DISPATCH                 00135**6
         BNE   RETURN                  NO, RETURN                       00136**5
         CLC   JCTLINES,=F'2000000'    IS JCT LINES > 2 MILLION LNS.    00137**5
         BH    RETURN                  YES, RETURN                      00138**5
         L     R0,=X'20000000'         SET FLAG BYTES IN R0             00139**5
         B     RETURN8                 GO TO SET RETURN CODE TO 8       00140**5
CHKJOB   DS    0H                                                       00141**5
*                                                                       00142**5
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - * 00143**5
*  AT LIGHTING, PRODUCTION JOBS ARE SUBMITTED BY THE JOB SCHEDULER    * 00144**5
*  UCC7 (NOW CA7 FROM COMPUTER ASSOCIATES).  EXIT 2 IS USED TO PLACE  * 00145**5
*  THE ID OF THE SUBMITTER IN JCTINDEV WITH THE FORMAT THAT TSO       * 00146**5
*  SUBMITTERS ARE THE TSO ID AND AN * IN POSITION 8, STARTED TASK     * 00147**5
*  SUBMITTERS ARE THE STARTED TASK NAME WITH A PLUS SIGN IN POSITION  * 00148**5
*  8, AND JOB SUBMITTERS ARE THE JOBNAME.                             * 00149**5
* - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - * 00150**5
*                                                                       00151**5
         CLC   JCTINDEV,=C'UCC7   +'   PRODUCTION JOB???                00152**5
*        BE    CKLINES                 NO, GO TO KILL JOB               00153**9
*        CLC   JCTINDEV,=C'COMMAND+'   PRODUCTION JOB???                00154**9
         BNE   KILLJOB                 NO, GO TO KILL JOB               00155**8
CKLINES  DS    0H                                                       00156**8
         CLC   JCTLINES,=F'2500000'    IS JCT LINES < 2.5 MILLION LNS.  00157**5
         BL    RETURN                  YES, RETURN                      00158**5
KILLJOB  DS    0H                                                       00159**5
         L     R0,=X'80000002'         SET FLAG BYTES IN R0 FOR 722     00160**5
*                                      WITH A DUMP                      00161**5
RETURN8  DS    0H                      RETURN CALLERS REG 14            00162**5
         LA    R15,8                   SET RETURN CODE TO 8             00163**5
         EJECT                                                          00164
RETURN   DS    0H                      RETURN CALLERS REG 14            00165**2
         RETURN (14,12),RC=(15)        RETURN CALLERS REGISTERS         00166**3
         DROP R12                      DROP ADDRESSABILITY              00167
         EJECT                                                          00168
         LTORG                                                          00169
        $MODEND                                                         00170
         END                                                            00171
